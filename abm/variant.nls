
to-report getRandomInitVariant
  let stream_id variantSeedID
  ; TODO, read init_prop
  report "alpha"
end

to-report getInitRecoverVariant
  ; TODO, read init_prop
  report "alpha"
end

to simul_updateVariantParams
  let varData table:get variantData infectVariant
  
  set ownIllnessPeriod_raw ( exp random-normal table:get varData "durationMean" table:get varData "durationStd" ) ;; log transform of illness period
  set ownIncubationPeriod_raw ( exp random-normal table:get varData "incubationMean" table:get varData "incubationStd" ) ;;; log transform of incubation period
  set ownIllnessPeriod ownIllnessPeriod_raw
  set ownIncubationPeriod ownIncubationPeriod_raw
  
  set nonInfectivePeriod table:get varData "non_infect_time"
  set Asymptomatic_Trans table:get varData "asympt_trans"
  
  let varAsymptProp table:get varData "asympt_prop"
  ifelse asymptomatic_prop_mult <= 1
  [
    set asymptomatic_prop varAsymptProp * asymptomatic_prop_mult
  ]
  [
    set asymptomatic_prop 1 - (1 - varAsymptProp) / asymptomatic_prop_mult
  ]
  
  set personalVirulence rngs:rnd-beta moveInitSeedID (table:get varData "trans_a" / hetro_mult) (table:get varData "trans_b" / hetro_mult)
  
  ;; Vaccine and previous infection
  set personalVirulence personalVirulence * (vaccine_getWanedParameter "infectReduct" infectVariant currentVaccine vaccineDay prevVaccine prevVaccineDay)
  set personalVirulence personalVirulence * (vaccine_getWanedParameter "infectReduct" infectVariant recoverVaccine recoverDay 0 0)
end

to setup_variantData
  let stream_id variantSeedID
  
  let inputFile (word input_variant)
  let variantFileData remove-item 0 csv:from-file inputFile
  
  set variantNames []
  set variantData table:make
  
  foreach variantFileData [ x ->
    let name (item 0 x)
    set variantNames lput name variantNames
    let data table:make
    
    let trans_mean        (item 1 x)
    let trans_std         (item 2 x)
    let asympt_trans_a    (item 3 x)
    let asympt_trans_b    (item 4 x)
    let asympt_prop_a     (item 5 x)
    let asympt_prop_b     (item 6 x)
    let non_infect_time   (item 7 x)
    let incubation_mean   (item 8 x)
    let incubation_spread (item 9 x)
    let duration_mean     (item 10 x)
    let duration_spread   (item 11 x)
    let init_variant      (item 12 x)
    
    ; Parameters for simulants to draw their own transmissibility
    let trans_n trans_mean * ( 1 - trans_mean ) / (trans_std ^ 2)
    table:put data "trans_a" trans_mean * trans_n
    table:put data "trans_b" (1 - trans_mean) * trans_n
    
    table:put data "asympt_trans" rngs:rnd-beta stream_id asympt_trans_a asympt_trans_b
    table:put data "asympt_prop" rngs:rnd-beta stream_id asympt_prop_a asympt_prop_b
    
    ;; illness period estimation using ln transform
    let BetaIllnessPd ln (1 + (duration_spread / duration_mean ^ 2))
    table:put data "durationMean" (ln duration_mean) - (BetaillnessPd / 2)
    table:put data "durationStd" sqrt BetaIllnessPd
    
    ;; illness period estimation using ln transform
    let BetaIncubationPd ln (1 + (incubation_spread / incubation_mean ^ 2))
    table:put data "incubationMean" (ln incubation_mean) - (BetaincubationPd / 2)
    table:put data "incubationStd" sqrt BetaIncubationPd
    
    table:put data "non_infect_time" non_infect_time
    table:put data "init_variant" init_variant
    
    table:put variantData name data
    table:put recoverCountByVariant name 0
  ]
  ;print (word "variantNames " variantNames)
  ;print (word "variantData " variantData)
end

to simul_updatepersonalvirulence
  ;; creates a triangular distribution of virulence that peaks at the end of the incubation period
  if color = red and (isInitialInfection or not first_case_calibrate) [
    if timenow <= nonInfectivePeriod [
      set currentVirulence 0
    ]
    ifelse ownIllnessPeriod_raw > 0
    [
      if timenow > nonInfectivePeriod and timenow <= ownIncubationPeriod [
        set currentVirulence personalVirulence * (((timenow - nonInfectivePeriod) / (ownIncubationPeriod - nonInfectivePeriod) ) * (1 - prepeak_vir_boost) + prepeak_vir_boost)
      ]
      if timenow > ownIncubationPeriod [
        set currentVirulence personalVirulence * (( ownIllnessPeriod - timenow ) / ( ownIllnessPeriod - ownIncubationPeriod ))
      ]
    ]
    [
      set currentVirulence 0
    ]
    ;if vaccinated = 1 [
    ;  show (word "cine " vaccinated ",time " timenow ",VIR " (currentVirulence / personalVirulence) ",unscale " currentVirulence)
    ;]
    ;set currentVirulence 0
    ;show (word "timenow " timenow ", personalVirulence " personalVirulence ", ownIncubationPeriod " ownIncubationPeriod ", ownIllnessPeriod " ownIllnessPeriod)
    ;show (word "timenow " timenow ", currentVirulenceProp " (currentVirulence / personalVirulence))
  ]
end
