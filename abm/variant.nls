
to-report getRandomInitVariant
  report base_name
end

to-report getInitRecoverVariant
  report base_name
end

to simul_updateVariantParams
  let varData table:get variantData infectVariant
  
  set ownIllnessPeriod_raw ( exp random-normal table:get varData "durationMean" table:get varData "durationStd" ) ;; log transform of illness period
  set ownIncubationPeriod_raw ( exp random-normal table:get varData "incubationMean" table:get varData "incubationStd" ) ;;; log transform of incubation period

  set nonInfectivePeriod table:get varData "non_infect_time"
  set Asymptomatic_Trans table:get varData "asympt_trans"
  
  set personalVirulence rngs:rnd-beta moveInitSeedID (table:get varData "trans_a" / hetro_mult) (table:get varData "trans_b" / hetro_mult)
  ;print (word "rawBat " ownIllnessPeriod_raw ", rawIll " ownIllnessPeriod_raw ", rawV " personalVirulence)
  
  ;; Vaccine and previous infection
  let infectivityReduction (vaccine_getWanedParameter "infectReduct" infectVariant currentVaccine vaccineDay prevVaccine prevVaccineDay) * (vaccine_getWanedParameter "infectReduct" infectVariant recoverVaccine recoverDay 0 0)
  let sqrtInfectivityReduction sqrt infectivityReduction
  
  set ownIllnessPeriod ownIllnessPeriod_raw * sqrtInfectivityReduction
  set ownIncubationPeriod ownIncubationPeriod_raw * sqrtInfectivityReduction
  set personalVirulence personalVirulence * sqrtInfectivityReduction
  
  ;print (word "Bat " ownIncubationPeriod ", Ill " ownIllnessPeriod ", V " personalVirulence ", Red " infectivityReduction)
  
  if self_iso_at_peak and symtomatic_present_day > -1 [
    set symtomatic_present_day floor (ownIncubationPeriod + pre_present_iso + random-float 1)
  ]
end

to setup_variantData
  let stream_id variantSeedID
  
  let inputFile (word input_variant)
  let variantFileData remove-item 0 csv:from-file inputFile
  file-close
  
  set variantNames []
  set variantData table:make
  let correlateAsympt table:make
  let transMeanCorrelatedDraw rngs:rnd-beta stream_id 1 1
  
  foreach variantFileData [ x ->
    let name (item 0 x)
    set variantNames lput name variantNames
    let data table:make
    
    let index 0 ;; Name is set above
    set index index + 1    let trans_min         (item index x)
    set index index + 1    let trans_max         (item index x)
    set index index + 1    let trans_std         (item index x)
    set index index + 1    let asympt_trans_mean (item index x)
    set index index + 1    let asympt_trans_std  (item index x)
    set index index + 1    let non_infect_time   (item index x)
    set index index + 1    let incubation_mean   (item index x)
    set index index + 1    let incubation_spread (item index x)
    set index index + 1    let duration_mean     (item index x)
    set index index + 1    let duration_spread   (item index x)
    set index index + 1    let hospLengthMult    (item index x)
    set index index + 1    let init_variant      (item index x)
    set index index + 1    let recoverVac        (item index x)
    set index index + 1    let correlateKey      (item index x)
    
    if trans_override > 0 [
      set trans_min trans_override 
      set trans_max trans_override
    ]
    
    let trans_mean trans_min + (trans_max - trans_min) * transMeanCorrelatedDraw
    ; TODO Output transmissibility draw potentially
    ;print (word name ", " trans_mean)
    
    ; Parameters for simulants to draw their own transmissibility
    let trans_n trans_mean * ( 1 - trans_mean ) / (trans_std ^ 2)
    table:put data "trans_a" trans_mean * trans_n
    table:put data "trans_b" (1 - trans_mean) * trans_n
    
    ;; Draw asymptomatic transmissibility factor.
    if not table:has-key? correlateAsympt correlateKey [
      let asymptValue exp (rngs:rnd-norm stream_id asympt_trans_mean asympt_trans_std)
      set sen_asymptTransmit_out asymptValue
      table:put correlateAsympt correlateKey asymptValue
    ]
    table:put data "asympt_trans" table:get correlateAsympt correlateKey ; TODO Output as sensitive f
    
    ;; illness period estimation using ln transform
    let BetaIllnessPd ln (1 + (duration_spread / duration_mean ^ 2))
    table:put data "durationMean" (ln duration_mean) - (BetaillnessPd / 2)
    table:put data "durationStd" sqrt BetaIllnessPd
    
    ;; illness period estimation using ln transform
    let BetaIncubationPd ln (1 + (incubation_spread / incubation_mean ^ 2))
    table:put data "incubationMean" (ln incubation_mean) - (BetaincubationPd / 2)
    table:put data "incubationStd" sqrt BetaIncubationPd
    
    table:put data "non_infect_time" non_infect_time
    table:put data "init_variant" init_variant
    table:put data "recoverVac" recoverVac
    table:put data "hospLengthMult" hospLengthMult
    
    table:put variantData name data
    table:put recoverCountByVariant name 0
  ]
  ;print (word "variantNames " variantNames)
  ;print (word "variantData " variantData)
end

to-report getVariantRecoverVaccine [variant]
  let data table:get variantData variant
  report (word table:get data "recoverVac" cohortVacSuffix)
end

to simul_updatepersonalvirulence
  ;; creates a triangular distribution of virulence that peaks at the end of the incubation period
  if color = red and (isInitialInfection or not first_case_calibrate) [
    (ifelse timenow <= nonInfectivePeriod 
      [
        set currentVirulence 0
      ]
      timenow < ownIncubationPeriod
      [
        set currentVirulence personalVirulence * (((timenow - nonInfectivePeriod) / (ownIncubationPeriod - nonInfectivePeriod) ) * (1 - prepeak_vir_boost) + prepeak_vir_boost)
      ]
      timenow < ownIllnessPeriod
      [
        set currentVirulence personalVirulence * (( ownIllnessPeriod - timenow ) / ( ownIllnessPeriod - ownIncubationPeriod ))
      ]
      [
        set currentVirulence 0
      ]
    )
  ]
    ;if vaccinated = 1 [
    ;  show (word "cine " vaccinated ",time " timenow ",VIR " (currentVirulence / personalVirulence) ",unscale " currentVirulence)
    ;]
    ;set currentVirulence 0
    ;show (word "timenow " timenow ", personalVirulence " personalVirulence ", ownIncubationPeriod " ownIncubationPeriod ", ownIllnessPeriod " ownIllnessPeriod)
    ;show (word "timenow " timenow ", currentVirulenceProp " (currentVirulence / personalVirulence))
end
