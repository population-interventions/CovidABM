
to setup_vaccineData
  let vaccineFileData remove-item 0 csv:from-file "input/vaccine.csv"
  set vaccineTable table:make
  let i 0
  foreach vaccineFileData [ x -> 
    table:put vaccineTable i x
    set i i + 1
  ]
  
  set vaccinePhaseIndex -1 ; Not yet initialised
  set vaccinePhaseEndDay 0
  
  set global_vaccinePhase 0
  set global_vaccineSubPhase 0
  set global_vaccineAvailible 0
  set global_vaccineType 0
  set global_vaccinePerDay 0
  set global_vaccine_eff 0
end

to vaccine_brand_update
  if global_vaccineType = "generic" [
    set global_vaccine_eff param_vac_tran_reduct
  ]
end

to vaccine_update
  ;; Happens at the start of the first 'go', since days = 0
  if days >= vaccinePhaseEndDay and param_vac_rate_mult > 0 [
    if table:has-key? vaccineTable (vaccinePhaseIndex + 1) [
      set vaccinePhaseIndex vaccinePhaseIndex + 1
      let phaseData table:get vaccineTable vaccinePhaseIndex
      if param_final_phase = -1 or (item 0 phaseData) <= param_final_phase [
        
        ;print "Old vaccine total pop and unvaccinated pop"
        ;print count simuls with [VaccPhase = global_vaccinePhase and VaccSubPhase = global_vaccineSubPhase]
        ;print count simuls with [VaccPhase = global_vaccinePhase and VaccSubPhase = global_vaccineSubPhase and vaccinated = 0]
        
        set global_vaccinePhase item 0 phaseData
        let phaseDays (item 2 phaseData) 
        let phaseAtFixedSpeed item 4 phaseData
        if phaseAtFixedSpeed = 0 [
          set phaseDays phaseDays / param_vac_rate_mult
        ]
        set global_vaccineSubPhase item 1 phaseData
        set global_vaccineType item 3 phaseData
        set global_vaccineUptakeOverride item 5 phaseData
        
        ;; Sum simulants in the vaccination phase.
        let phasePop 0
        foreach popDivisionTable_keys [ pop_index ->
          let cohortData table:get popDivisionTable pop_index
          let cohort_phase item 6 cohortData
          let cohort_subPhase item 7 cohortData
          if cohort_phase = global_vaccinePhase and cohort_subPhase = global_vaccineSubPhase [
            ;; Add population and subtract already vaccinated.
            set phasePop phasePop + (item 0 cohortData) - (item 13 cohortData)
          ]
          if global_vaccineUptakeOverride > 0 [
            let cohort table:get populationCohortCache pop_index
            ask cohort with [ vaccinated = 0 and vaccineOffered = 1 and sm_vac_uptake < global_vaccineUptakeOverride ] [
              set phasePop phasePop + 1
              set vaccineOffered 0
            ]
          ]
        ]
        ;print phasePop
        
        set vaccinePhaseEndDay (vaccinePhaseEndDay + phaseDays)
        set global_vaccineAvailible 0
        set global_vaccinePerDay phasePop / phaseDays + 0.00001 ; Floats
        
        vaccine_brand_update
        
        ;print "New vaccine phase"
        ;print global_vaccinePhase
        ;print global_vaccineSubPhase
        ;print global_vaccinePerDay
        ;print vaccinePhaseEndDay
      ]
    ]
  ]
  
  set global_vaccineAvailible global_vaccineAvailible + global_vaccinePerDay
end


to simul_vaccinate_me
  if vacWaitDays > 0 [
    set vacWaitDays vacWaitDays - 1
    if vacWaitDays = 0 [
      set vaccinated 1
      simul_updateVacAndReinfectMult
    ]
  ]
  
  if Vaccine_Available = true and global_vaccineAvailible >= 1 [
    if VaccPhase <= global_vaccinePhase and VaccSubPhase <= global_vaccineSubPhase and vaccinated = 0 and vaccineOffered = 0 [
      ;; TODO for some reason people use up a vaccine even if they refuse it, but if this wasn't the case then param_vac_uptake
      ;; would mean very little. Figure out what is going on here conceptually.
      set vaccineOffered 1
      set selfVaccEff_raw global_vaccine_eff
      set global_vaccineAvailible global_vaccineAvailible - 1
      ifelse sm_vac_uptake < param_vac_uptake
      [
        set vacWaitDays param_vacEffDays
      ]
      [
        if ignoreUptakeOverride = 0 and global_vaccineUptakeOverride > 0 [
          if sm_vac_uptake < global_vaccineUptakeOverride [
            set vacWaitDays param_vacEffDays
          ]
        ]
      ]
    ]
  ]
end


to simul_vaccinate_me_force
  if Vaccine_Available = true [
    set vaccineOffered 1
    set selfVaccEff_raw global_vaccine_eff
    set vaccinated 1
    simul_updateVacAndReinfectMult
  ]
end
