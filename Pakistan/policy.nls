
to update_policy_trigger_scale
  if global_vaccinePhase = 2 [
    if global_vaccineSubPhase = 1 [
      set policyTriggerScale 2
    ]
    if global_vaccineSubPhase = 2 [
      set policyTriggerScale 4
    ]
  ]
  if global_vaccinePhase >= 3 [
    set policyTriggerScale 8
  ]
end

;;;*********************************************************************************************************************
;; Agressive/Moderate Elimination, and Tight/Loose Supression
;;;*********************************************************************************************************************

to policy_agressive_elim
  (ifelse stage <= 3 and casesinperiod7 > 7.5 * 7 * policyTriggerScale
    [
      set stage 4  
      set decisionDate ticks
    ]
    stage <= 2 and casesinperiod7 > 1.5 * 7 * policyTriggerScale
    [
      set stage 3  
      set decisionDate ticks
    ]
    stage <= 1 and casesinperiod14 >= 2 * policyTriggerScale
    [
      set stage 2  
      set decisionDate ticks
    ]
    stage <= 0 and casesinperiod7 >= 1 * policyTriggerScale
    [
      set stage 1  
      set decisionDate ticks
    ]
  )
  
  if decisionDate + 21 <= ticks [
    (ifelse stage >= 4 and casesinperiod7 < 5 * 7 * policyTriggerScale
      [
        set stage 3 
        set decisionDate ticks
      ]
      stage >= 3 and casesinperiod7 < 1 * 7 * policyTriggerScale
      [
        set stage 2  
        set decisionDate ticks
      ]
      stage >= 2 and casesinperiod7 <= 0 and casesinperiod14 < 2 * policyTriggerScale
      [
        set stage 1  
        set decisionDate ticks
      ]
      stage >= 1 and casesinperiod28 <= 0
      [
        set stage 0  
        set decisionDate ticks
      ]
    )
  ]
end

to policy_moderate_elim
  (ifelse stage <= 3 and casesinperiod7 > 30 * 7 * policyTriggerScale
    [
      set stage 4  
      set decisionDate ticks
    ]
    stage <= 2 and casesinperiod7 > 6 * 7 * policyTriggerScale
    [
      set stage 3  
      set decisionDate ticks
    ]
    stage <= 1 and casesinperiod7 > 1 * 7 * policyTriggerScale
    [
      set stage 2  
      set decisionDate ticks
    ]
    stage <= 0 and casesinperiod7 >= 2 * policyTriggerScale
    [
      set stage 1  
      set decisionDate ticks
    ]
  )
  
  if decisionDate + 21 <= ticks [
    (ifelse stage >= 4 and casesinperiod7 < 20 * 7 * policyTriggerScale
      [
        set stage 3
        set decisionDate ticks
      ]
      stage >= 3 and casesinperiod7 < 5 * 7 * policyTriggerScale
      [
        set stage 2  
        set decisionDate ticks
      ]
      stage >= 2 and casesinperiod7 < 1 * 7 * policyTriggerScale
      [
        set stage 1  
        set decisionDate ticks
      ]
      stage >= 1 and casesinperiod7 <= 0
      [
        set stage 0  
        set decisionDate ticks
      ]
    )
  ]
end

to policy_tight_supress
  (ifelse allowStage4 and stage <= 3 and casesinperiod7 > 1395 * total_population / 1000000 * policyTriggerScale
    [
      set stage 4  
      set decisionDate ticks
    ]
    stage <= 2 and casesinperiod7 > 1163 * total_population / 1000000  * policyTriggerScale
    [
      set stage 3  
      set decisionDate ticks
    ]
    stage <= 0 and casesinperiod7 >= 465 * total_population / 1000000 * policyTriggerScale 
    [
      set stage 1
      set decisionDate ticks
    ]
  )
  
  if decisionDate + 21 <= ticks [
    (ifelse stage >= 4 and casesinperiod7 < 930 * total_population / 1000000 * policyTriggerScale
      [
        set stage 3
        set decisionDate ticks
      ]
      stage >= 3 and casesinperiod7 < 465 * total_population / 1000000 * policyTriggerScale
      [
        set stage 1
        set decisionDate ticks
      ]
      stage >= 1 and casesinperiod7 < 232.5 * total_population / 1000000 * policyTriggerScale
      [
        set stage 0
        set decisionDate ticks
      ]
    )
  ]
end

to policy_moderate_supress
  (ifelse allowStage4 and stage <= 3 and casesinperiod7 > 12600 * policyTriggerScale
    [
      set stage 4  
      set decisionDate ticks
    ]
    stage <= 2 and casesinperiod7 > 6300 * policyTriggerScale
    [
      set stage 3  
      set decisionDate ticks
    ]
    stage <= 0 and casesinperiod7 >= 3150 * policyTriggerScale
    [
      set stage 1
      set decisionDate ticks
    ]
  )
  
  if decisionDate + 21 <= ticks [
    (ifelse stage >= 4 and casesinperiod7 < 8400 * policyTriggerScale
      [
        set stage 3
        set decisionDate ticks
      ]
      stage >= 3 and casesinperiod7 < 4200 * policyTriggerScale
      [
        set stage 1
        set decisionDate ticks
      ]
      stage >= 1 and casesinperiod7 < 2100 * policyTriggerScale
      [
        set stage 0
        set decisionDate ticks
      ]
    )
  ]
end

;;;*********************************************************************************************************************
;;; Stage Calibration
;;;*********************************************************************************************************************

to policy_none
  set stage -1
end

to policy_stage [ toStage ]
  set Vaccine_Available false
  if ticks = 0 [
    set stage -1
    set End_Day -1
    set track_R False
  ]
  
  if End_Day = -1 and casesinperiod7 >= calibrate_stage_switch [
    set stage toStage
    set End_Day ticks + 70
    set track_R True
    set R_measure_time ticks + 30
    set decisionDate ticks
  ]
end

;;;*********************************************************************************************************************
;;;*********************************************************************************************************************

to COVIDPolicyTriggers
  if param_trigger_loosen [
    update_policy_trigger_scale 
  ]
  if param_policy = "TightSupress" [
    set allowStage4 true
    policy_tight_supress
    set Vaccine_Available true
  ]
  if param_policy = "ModerateSupress" [
    set allowStage4 true
    set Vaccine_Available true
    policy_moderate_supress
  ]
  if param_policy = "TightSupress_No_4" [
    set allowStage4 false
    set Vaccine_Available true
    policy_tight_supress
  ]
  if param_policy = "ModerateSupress_No_4" [
    set allowStage4 false
    set Vaccine_Available true
    policy_moderate_supress
  ]
  if param_policy = "AggressElim" [
    policy_agressive_elim
    set Vaccine_Available true
  ]
  if param_policy = "ModerateElim" [
    policy_moderate_elim
    set Vaccine_Available true
  ]
  if param_policy = "None" [
    policy_none
    set Vaccine_Available false
  ]
  if param_policy = "Stage 1" [
    set stage 0
    set Vaccine_Available false
  ]
  if param_policy = "Stage 1b" [
    set stage 1 
    set Vaccine_Available false
  ]
  if param_policy = "Stage 2" [
    set stage 2 
    set Vaccine_Available false
  ]
  if param_policy = "Stage 3" [
    set stage 3 
    set Vaccine_Available false
  ]
  if param_policy = "Stage 4" [
    set stage 4 
    set Vaccine_Available false
  ]
  if param_policy = "StageCal None" [
    policy_stage -1
  ]
  if param_policy = "StageCal Test" [
    policy_stage -2
  ]
  if param_policy = "StageCal_1" [
    policy_stage 0
  ]
  if param_policy = "StageCal_1b" [
    policy_stage 1
  ]
  if param_policy = "StageCal_2" [
    policy_stage 2
  ]
  if param_policy = "StageCal_3" [
    policy_stage 3
  ]
  if param_policy = "StageCal_4" [
    policy_stage 4
  ]
end
