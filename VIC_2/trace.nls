
to simul_setContactsKnown
  ;; Allow contacts to be tracked now that this case is tracked.
  set hasKnownContact true
  ask turtle-set (table:values infectedContactList) [
    set hasKnownContact true
  ]
end

to simul_fixInfectedContactList
  ;; Remove duplicate and non-infected simulants from contact list
  let idTable table:make
  let fixedTable table:make
  let anyKnownContact false
  ask turtle-set (table:values infectedContactList) [
    if not table:has-key? idTable who [
      table:put idTable who 1
      if color = red [
        table:put fixedTable (table:length fixedTable) self
        if hasKnownContact [
          set anyKnownContact true
        ]
      ]
    ]
  ]
  set infectedContactList fixedTable
  
  set hasKnownContact (anyKnownContact or hasKnownContact)
  if tracked = 1 [
    simul_setContactsKnown
  ]
end

to simul_traceme
  ;; this represents the standard tracking and tracing regime - undetected people are not tracked
  ;; Symtomatic people automatically trace themselves on symtomatic_present_day of their infection (with the day of infection being day 0) 
  if color = red [
    if asymptomaticFlag = 0 and noticeOwnInfection = 0 and (symtomatic_present_day > -1 and timenow >= symtomatic_present_day) [
      set noticeOwnInfection 1
    ]
    if tracking = true and tracked != 1 [
      if color = red and tracked = 0 and (
            (asymptomaticFlag = 1 and hasKnownContact and Asymptom_Trace_Mult * track_and_trace_efficiency > random-float 1)
          or 
            (asymptomaticFlag = 0 and (
              noticeOwnInfection = 1
            or
              (hasKnownContact and track_and_trace_efficiency > random-float 1)
            ))
          ) [
        set caseReportTime ticks + Case_Reporting_Delay
        set tracked 1
        set IDTime timenow
        set justTracked true
      ]
    ]
  ]
end

to simul_updateContactTracing
  if justTracked [
    set justTracked false
    simul_setContactsKnown
  ]
end

to test_traceadjust
  print traceMult
  set infectionsinperiod7 0
  traceadjust
  print list (infectionsinperiod7 / 7) track_and_trace_efficiency
  set infectionsinperiod7 1 * 7
  traceadjust
  print list (infectionsinperiod7 / 7) track_and_trace_efficiency
  set infectionsinperiod7 5 * 7
  traceadjust
  print list (infectionsinperiod7 / 7) track_and_trace_efficiency
  set infectionsinperiod7 10 * 7
  traceadjust
  print list (infectionsinperiod7 / 7) track_and_trace_efficiency
  set infectionsinperiod7 50 * 7
  traceadjust
  print list (infectionsinperiod7 / 7) track_and_trace_efficiency
  set infectionsinperiod7 100 * 7
  traceadjust
  print list (infectionsinperiod7 / 7) track_and_trace_efficiency
end

to traceadjust
  ifelse infectionsinperiod7 > 0
  [
    set track_and_trace_efficiency  (0.88 ^ ln((infectionsinperiod7 / 7) ^ 2)) * 0.25 * traceMult * param_trace_mult
  ]
  [
    set track_and_trace_efficiency  (0.88 ^ ln((1 / 7) ^ 2)) * 0.25 * traceMult * param_trace_mult
  ]
end
